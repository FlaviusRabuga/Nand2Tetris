// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    // process the reset flag
    Mux16(a= in, b= false, sel= reset, out= afterReset);

    // process the inc flag
    Or(a= reset, b= load, out= rOrl);
    Not(in= rOrl, out= shouldInc);
    Inc16(in= prevVal, out= prevValInc);
    Mux16(a= afterReset, b= prevValInc,
          sel= shouldInc, out= afterInc);

    // update the register
    Or(a= rOrl, b= inc, out= shouldUpdate);
    Register(in= afterInc, load= shouldUpdate,
             out= prevVal, out= out);
}













