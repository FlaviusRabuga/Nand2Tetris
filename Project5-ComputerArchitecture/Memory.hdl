// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/Memory.hdl
/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid and reads 0. Access to any address
 * in the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    /*
    memory chip selection algorithm:
        if 000000000000000 <= address <= 011111111111111:
            select RAM
        if 100000000000000 <= address <= 101111111111111:
            select Screen Memory-Map
        if address == 110000000000000:
            select Keyboard Memory-Map
        else:
            return 0000000000000000
    */

    /*
    simplified algorithm:
        if address[14] == 0:
            select RAM
        if address[14] == 1 and address[13] == 0:
            select Screen Memory-Map
        if address == 110000000000000:
            select Keyboard Memory-Map
        else:
            return 0000000000000000
    */

    // if address[14] == 0:
    Not(in= address[14], out= selectRAM, out= notAddressBit14);

    // if address[14] == 1 and address[13] == 0:
    Not(in= address[13], out= notAddressBit13);
    And(a= address[14], b= notAddressBit13, out= selectScreen);

    // if address == 110000000000000:
    Or8Way(in= address[0..7], out= orHalf0);
    Or8Way(in[0..4]= address[8..12],
           in[5]= notAddressBit13, in[6] =notAddressBit14, in[7]= false,
           out= orHalf1);
    Or(a= orHalf0, b= orHalf1, out= orFull);
    Not(in= orFull, out= selectKeyboard);

    // memory chips declarations
    And(a= selectRAM, b= load, out= loadRAM);
    RAM16K(in= in, load= loadRAM, address= address[0..13], out= outRam);

    And(a= selectScreen, b= load, out= loadScreen);
    Screen(in= in, load= loadScreen, address= address[0..12], out= outScreen);

    Keyboard(out= outKeyboard);

    // select the right output
    Mux16(a= false, b= outKeyboard, sel= selectKeyboard, out= outUpperLevel);
    Mux4Way16(a= outRam, b= outRam,
              c= outScreen, d= outUpperLevel,
              sel[0]= address[13], sel[1]= address[14], 
              out= out);
}
